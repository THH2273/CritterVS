import org.jetbrains.kotlin.gradle.tasks.KotlinJvmCompile

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        // Make sure this version matches the one included in Kotlin for Forge
        classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.9.22'
        // OPTIONAL Gradle plugin for Kotlin Serialization
        classpath 'org.jetbrains.kotlin:kotlin-serialization:1.9.22'
    }
}

plugins {
    id 'dev.architectury.loom' version '1.3.355'
    id 'architectury-plugin' version '3.4.146'
    id "org.jetbrains.kotlin.jvm" version "$kotlin_version"

    id 'com.matthewprenger.cursegradle' version '1.4.0'
    id "com.modrinth.minotaur" version "2.4.5"
    id "com.github.johnrengelman.shadow" version "7.1.2"
}

// Version determinance in line with VS conventions
if (project.hasProperty("CustomReleaseVersion")) {
    // Remove release/ from the version if present
    version = project.property("CustomReleaseVersion").replaceFirst("^release/", "")
} else {
    String gitRevision = "git rev-parse HEAD".execute().text.trim()
    if (gitRevision.isEmpty()) {
        gitRevision = 'unknown'
    }

    version = mod_version + '+' + (gitRevision.length() >= 10 ? gitRevision.substring(0, 10) : gitRevision)
}

architectury {
    minecraft = project.minecraft_version
    platformSetupLoomIde()
    forge()
}

allprojects {
//    apply plugin: "java"
//    apply plugin: "kotlin"
//    apply plugin: "architectury-plugin"
//    apply plugin: "maven-publish"
//    apply plugin: 'dev.architectury.loom'
//    apply plugin: "org.jetbrains.kotlin.jvm"

    archivesBaseName = rootProject.archives_base_name
    version = rootProject.version
    group = rootProject.maven_group

    // Copied from VS oddities. Forces arch to use the right version (which it doesn't for some reason on some machines)
    configurations.configureEach {
        resolutionStrategy.force "org.lwjgl:lwjgl-stb:3.3.1"
        resolutionStrategy.force "org.lwjgl:lwjgl-opengl:3.3.1"
        resolutionStrategy.force "org.lwjgl:lwjgl-openal:3.3.1"
        resolutionStrategy.force "org.lwjgl:lwjgl-tinyfd:3.3.1"
        resolutionStrategy.force "org.lwjgl:lwjgl-jemalloc:3.3.1"
        resolutionStrategy.force "org.lwjgl:lwjgl-glfw:3.3.1"
        resolutionStrategy.force "org.lwjgl:lwjgl:3.3.1"
    }

    repositories {
        //Add any repositories to fetch dependencies from here.
        //Most dependencies are mirrored to the VS Maven, so by default only it is included alongside mavenLocal and KFF.
        mavenLocal()
        maven {
            name = "Valkyrien Skies Internal"
            url = project.vs_maven_url ?: 'https://maven.valkyrienskies.org'
            if (project.vs_maven_username && project.vs_maven_password) {
                credentials {
                    username = project.vs_maven_username
                    password = project.vs_maven_password
                }
            }
        }
        maven {
            name = 'Kotlin for Forge'
            url = 'https://thedarkcolour.github.io/KotlinForForge/'
            content { includeGroup "thedarkcolour" }
        }
    }

    loom {
        silentMojangMappingsLicense()
    }

    dependencies {
        minecraft "net.minecraft:minecraft:$rootProject.minecraft_version"
        mappings loom.officialMojangMappings()
    }

    java {
        withSourcesJar()
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = "UTF-8"

        options.release = 17
    }

    tasks.withType(KotlinJvmCompile).configureEach {
        kotlinOptions {
            jvmTarget = "17"
            freeCompilerArgs += "-Xjvm-default=all"

        }
    }

    String buildNumber = System.getenv("GITHUB_RUN_NUMBER")
}

dependencies {
    forge("net.minecraftforge:forge:${minecraft_version}-${forge_version}")

    include(modApi("dev.architectury:architectury-forge:${rootProject.architectury_api_version}"))
    implementation(include((("io.github.llamalad7:mixinextras-forge:0.4.1"))))

    //VS2 Dependencies. The latest VS2 and VSCore versions can be retrieved from the Valkyrien Skies Github repository.
    //The version tag is usually the major version, followed by the first 10 characters of the git commit hash.
    //The VSCore version tag can be grabbed directly from the gradle.properties file in the Valkyrien skies repo.
    modApi("org.valkyrienskies:valkyrienskies-120-forge:${rootProject.vs2_version}") { transitive = false
        exclude group: 'org.valkyrienskies.core', module: ''
        exclude group: "com.simibubi"
        exclude group: "io.github.fabricators_of_create"
        exclude group: "dev.engine-room"
        exclude group: "dev.engine_room"
    }
    implementation("org.valkyrienskies.core:api:${rootProject.vscore_version}") { transitive = false
        exclude group: 'org.joml', module: ''
    }
    implementation("org.valkyrienskies.core:util:${rootProject.vscore_version}") { transitive = false
        exclude group: 'org.joml', module: ''
    }
    implementation("org.valkyrienskies.core:internal:${rootProject.vscore_version}") { transitive = false
        exclude group: 'org.joml', module: ''
    }

    // Kotlin
    implementation("thedarkcolour:kotlinforforge:${rootProject.kotlin_forge}")

    compileOnly("org.joml:joml:1.10.4")
    compileOnly("org.joml:joml-primitives:1.10.0")
}

processResources {
    filesMatching("META-INF/mods.toml") {
        expand "version": version
    }
}

loom {
    forge {
        mixinConfig "vs_template.mixins.json"
        convertAccessWideners.set(true)
    }
    mixin {
        defaultRefmapName = "vs_template-refmap.json"
    }
}

tasks.withType(JavaCompile).configureEach {
    // Minecraft 1.18 (1.18-pre2) upwards uses Java 17.
    it.options.release = 17
}

shadowJar {
    archiveClassifier.set "dev-shadow"
}

remapJar {
    input.set shadowJar.archiveFile
    dependsOn shadowJar
    archiveClassifier.set null
}

jar {
    archiveClassifier.set "dev"
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

